# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Laurent Carlier <lordheavym@gmail.com>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Zephyr

pkgname=mupen64plus-c1
pkgver=2.0
pkgrel=1
pkgdesc='Nintendo64 Emulator for ODROID-C1'
arch=('armv7h')
url='http://code.google.com/p/mupen64plus/'
license=('GPL')
depends=('glu' 'libsamplerate' 'libpng' 'freetype2' 'boost-libs')
makedepends=('mesa' 'boost' 'sdl2-c1') 
source=("https://github.com/mupen64plus/mupen64plus-core/raw/master/tools/m64p_helper_scripts.tar.gz"
        "git+https://github.com/mdrjr/c1_mali_libs.git")
sha256sums=('SKIP'
            'SKIP')
conflicts=("mupen64plus") 
provides=("mupen64plus")

prepare() {
  cd "$srcdir"
  ./m64p_get.sh
}

build() {
  cd "$srcdir"
  MALI_INCLUDE=$srcdir/c1_mali_libs/fbdev/mali_headers
  CFLAGS="$CFLAGS -I$MALI_INCLUDE" 
  CPPFLAGS="$CPPFLAGS -DNOSSE -I$MALI_INCLUDE"
  LDFLAGS+=" -L/usr/lib/mali-egl -L/usr/lib/aml_libs"
  ./m64p_build.sh USE_GLES=1 NEON=1 OPTFLAGS=-O3 PIC=1 NO_SSE=1 USE_FRAMESKIPPER=1 HIRES=1 V=1
}

package() {
  cd "$srcdir"
  LDFLAGS+=" -L/usr/lib/mali-egl -L/usr/lib/aml_libs"
  # set LDCONFIG since we are using fakeroot and scripts run root commands by checking the uid
  ./m64p_install.sh DESTDIR="$pkgdir" PREFIX='/usr' MANDIR='/usr/share/man' LDCONFIG='true' USE_GLES=1 NEON=1 OPTFLAGS=-O3 PIC=1 NO_SSE=1 USE_FRAMESKIPPER=1 HIRES=1 V=1
}
