# Maintainer:  prettyvanilla <prettyvanilla@posteo.at>
# Contributor: almostalive <almostalive2003 at gmail dot com>
# Contributor: Holzhaus <holthuis.jan@googlemail.com>

pkgname=libretro-genesis-plus-gx-c1-git
pkgver=904.2b8f23a
pkgrel=1
pkgdesc="libretro implementation of Genesis Plus GX. (Sega Master System/Sega Game Gear/Sega Mega Drive/Sega Genesis/Sega CD)(for ODROID C1)"
arch=('armv7h')
url="https://github.com/libretro/Genesis-Plus-GX"
license=('custom')
conflicts=('libretro-genplus-git' 'libretro-genesis-plus-gx-git')
provides=('libretro-genesis-plus-gx')
depends=('zlib')
makedepends=('git')

_gitname=Genesis-Plus-GX
source=("git+https://github.com/libretro/${_gitname}.git"
        "https://raw.github.com/libretro/libretro-super/master/dist/info/genesis_plus_gx_libretro.info")
md5sums=('SKIP'
         'SKIP')

pkgver() {
  cd "${_gitname}"
  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd "${_gitname}"
  make -f Makefile.libretro platform=armv,cortexa5,hardfloat
}

package() {
  install -Dm644 "${_gitname}/sdl/LICENSE.txt" "${pkgdir}/usr/share/licenses/${pkgname}/license.txt"
  install -Dm644 "${_gitname}/genesis_plus_gx_libretro.so" "${pkgdir}/usr/lib/libretro/libretro-genplus.so"
  install -Dm644 "genesis_plus_gx_libretro.info" "${pkgdir}/usr/lib/libretro/libretro-genplus.info"
}
